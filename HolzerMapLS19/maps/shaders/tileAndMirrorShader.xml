<?xml version="1.0" encoding="utf-8"?>
<CustomShader version="4">
    <Parameters>
        <Parameter name="mirrorScaleAndOffsetUV" target="mirrorScaleAndOffsetUV" type="float4" defaultValue="1 1 0 0.5"/>
        <Parameter name="atlasInvSizeAndOffsetUV" target="atlasInvSizeAndOffsetUV" type="float4" defaultValue="0.5 0.5 0 0"/>
        <Parameter name="RDT" target="RDT" type="float4" defaultValue="1.0 1.0 20.0 20.0" minValue="0.0 0.0 0.0 0.0" maxValue="1.0 1.0 50.0 50.0"/>
    </Parameters>
    <Textures>
        <Texture name="DirtNmap" defaultColorProfile="linearRGB" defaultFilename="../textures/dirt_normal.png"/>
    </Textures>
    <Variations>
        <Variation name="DIRT_ENABLED"><![CDATA[
    #define DIRT_ENABLED 
]]></Variation>
    </Variations>
    <LodLevel startDistance="0">
        <CodeInjections>
            <CodeInjection position="NUM_TEXCOORDS"><![CDATA[

#if defined(DIFFUSE)
    #define DO_MIRROR_SHADER
#endif

#if defined(DO_MIRROR_SHADER)
    #if defined(NORMAL_MAP)
        // we need different normal map coordinates than albedo map coords
        #define NORMALMAP_TEXCOORD normalTexCoords
    #endif
    #if defined(GLOSS_MAP)
        #define GLOSSMAP_TEXCOORD glossmapTexCoords
    #endif
#endif
]]></CodeInjection>
            <CodeInjection position="VS_OUTPUT"><![CDATA[
#if defined(DO_MIRROR_SHADER)
    float2 normalTexCoords : TEXCOORDn;
    float2 glossmapTexCoords : TEXCOORDn;
#endif
]]></CodeInjection>
            <CodeInjection position="MATERIALINFO"><![CDATA[
uniform float4 mirrorScaleAndOffsetUV;
uniform float4 atlasInvSizeAndOffsetUV;
#if defined( DIRT_ENABLED )
    uniform float4 RDT;
#endif
]]></CodeInjection>
            <CodeInjection position="SAMPLERS"><![CDATA[
#if defined( DIRT_ENABLED )
    sampler2D DirtNmap;
#endif
]]></CodeInjection>
            <CodeInjection position="FS_GLOBALS"><![CDATA[
#if defined( DIRT_ENABLED )
    float4 gGloss;
    float  gAO;
    float  gDirt;
    float  maxSpecularIntensity;
#endif
]]></CodeInjection>
            <CodeInjection position="POST_SET_TEXCOORDS_VS"><![CDATA[
#if defined(DO_MIRROR_SHADER)
    float2 newUV = getDefaultTexCoords(In);
    Out.normalTexCoords     = newUV;
    Out.glossmapTexCoords   = newUV;
    // mirror
    newUV = ((newUV-mirrorScaleAndOffsetUV.zw)*mirrorScaleAndOffsetUV.xy)+mirrorScaleAndOffsetUV.zw;
    // atlas
    newUV = newUV*atlasInvSizeAndOffsetUV.xy + atlasInvSizeAndOffsetUV.zw;
    Out.defaultTexCoords = newUV.xy;
#endif
]]></CodeInjection>
            <CodeInjection position="START_FS"><![CDATA[
#if defined( DIRT_ENABLED )
    #if defined( GLOSS_MAP )
    /*
    glossMap.r - roughness
    glossMap.g - ambient occlusion
    glossMap.b - dirt mask
    glossMap.a - color mask ( if enabled )
    */
        globals.gGloss = tex2D( glossMap, In.vs.glossmapTexCoords).rgba;
        globals.gAO    = globals.gGloss.g;
        float texDirt  = 1 - globals.gGloss.b;
        globals.gDirt  = saturate( (texDirt*1.05 - (RDT.g - 0.5)) * 2 );
    #else
        globals.gGloss = 0;
        globals.gAO = 0;
        globals.gDirt = 0;
    #endif
    globals.maxSpecularIntensity = 0.5; // iron
#endif
]]></CodeInjection>
            <CodeInjection position="GET_UNNORMALIZED_TANGENT_SPACE_NORMAL_FS"><![CDATA[
#if defined( DIRT_ENABLED )
    #if defined( NORMAL_MAP )
        float3 mNormal;
        #ifdef NORMAL_MAP_SIGNED_2CHANNEL
            // use BC5 signed format for the normal map
            mNormal.xy = tex2D(normalMap, In.vs.NORMALMAP_TEXCOORD).xy;
            mNormal.z = sqrt(1.0 - dot(mNormal.xy, mNormal.xy));
        #else
            mNormal = tex2D(normalMap, In.vs.NORMALMAP_TEXCOORD).xyz - 0.5;
        #endif
        float3 mDirtNmap = tex2D( DirtNmap, float2( RDT.z, RDT.w ) * In.vs.NORMALMAP_TEXCOORD ).xyz - 0.5;
        return mNormal + float3(mDirtNmap.xy, 0.0) * (1 - globals.gDirt);
    #endif
#endif
]]></CodeInjection>
            <CodeInjection position="GET_TANGENT_SPACE_NORMAL_FS"><![CDATA[
#if defined( DIRT_ENABLED )
    #if defined( NORMAL_MAP )
        return normalize(getUnnormalizedTangentSpaceNormal(In, globals));
    #endif
#endif
]]></CodeInjection>
            <CodeInjection position="POST_ALBEDO_FS"><![CDATA[
#if defined( DIRT_ENABLED )
    #if defined( SPECULAR ) && defined( GLOSS_MAP )
        float3 mDirtColor = float3( 0.20, 0.14, 0.08 ); // HardCoded Dirt Color
        albedo.xyz = lerp( mDirtColor, albedo.xyz, globals.gDirt); // DirtColor lerp    
        albedo.xyz *= globals.gAO; // Multiply AO for DIFFUSE
    #endif
#endif
]]></CodeInjection>
            <CodeInjection position="POST_GLOSS_COLOR_FS"><![CDATA[
#if defined( DIRT_ENABLED )
    #if defined( SPECULAR ) && defined( GLOSS_MAP )
        float cosPower = max(cCosPower * globals.gGloss.r * globals.gDirt, 0.1);
        roughness = sqrt(sqrt(2.0/(cosPower+2.0)));
        glossColor.xyz = (float3)(globals.gGloss.r*globals.gDirt);
        glossColor.xyz *= globals.maxSpecularIntensity*globals.gAO; // Multiply AO for SPECULAR
    #endif
#endif
]]></CodeInjection>
        </CodeInjections>
    </LodLevel>
</CustomShader>
